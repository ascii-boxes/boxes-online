/*
 * boxes-online - A Web UI for the 'boxes' tool
 * Copyright (C) 2018  Thomas Jensen and the contributors
 *
 * This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public
 * License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later
 * version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 * warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 * details.
 *
 * You should have received a copy of the GNU General Public License along with this program; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
 */

buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: springBootVersion
    }
}

import com.thomasjensen.boxes.online.build.tasks.DesignListTask


plugins {
    id 'java'
    id 'idea'
}
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.thomasjensen.boxes.online'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    jcenter()
}

dependencies {
    compileOnly group: 'com.google.code.findbugs',   name: 'jsr305',           version: '3.0.1'
    compile     group: 'org.springframework.boot',   name: 'spring-boot-starter-web'
    compile     group: 'org.springframework.boot',   name: 'spring-boot-starter-log4j2'
    compile     group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    compile     group: 'org.zeroturnaround',         name: 'zt-exec',          version: '1.10'
    testCompile group: 'org.springframework.boot',   name: 'spring-boot-starter-test'
    testCompile group: 'org.assertj',                name: 'assertj-core',     version: '3.11.1'
}
configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation' << '-proc:none'
}


idea.module {
    downloadSources = true;
    downloadJavadoc = true;
    excludeDirs += file('.idea')
    excludeDirs += file('_support')
}

project.tasks.create('designList', DesignListTask.class);
